buildscript {
    repositories {
        jcenter()
    }

    dependencies{
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'
apply plugin: 'maven-publish'
apply plugin: com.jfrog.bintray.gradle.BintrayPlugin

if (file('bintray.gradle').exists()) apply from: 'bintray.gradle'

group 'com.scarlatti'

sourceCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
    flatDir dirs: ['lib']
}

dependencies {
    compile 'edu.sc.seis.gradle:launch4j:2.4.2'
    testCompile 'imcdonagh:image4j:0.7.2'
    compile 'com.talanlabs:avatar-generator:1.1.0'
    testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
        exclude module: 'groovy-all'
    }
}

task pluginDistributionZip(type: Zip) {
    group = 'distribution'
    description = 'Build the distribution zip for this plugin.'
    dependsOn build
    from jar.outputs.files
    baseName = jar.baseName
    destinationDir = file("/build/distributions")
}

task pluginDistributionJar(type: Copy) {
    group = 'distribution'
    description = 'Copy the plugin jar to the distribution dir.'
    dependsOn build
    from jar.outputs.files
    destinationDir = file("/build/distributions")
}

task pluginDistributionAssemble {
    dependsOn = [pluginDistributionZip, pluginDistributionJar]
    group = 'distribution'
    description = 'Put both the plugin .jar and .zip archives in the distribution dir.'
}

pluginDistributionJar.destinationDir

bintray {
    user = findProperty("bintrayUser")
    key = findProperty("bintrayKey")

    pkg {
        repo = 'maven'
        name = project.name
        licenses = ['MIT']
        vcsUrl = 'https://github.com/alessandroscarlatti/launch4j-gradle-plugin.git'

        version {
            name = project.version
            desc = 'Test uploading to bintray repo'
        }
    }

    publications = ['PluginPublication']
}

publishing {
    publications {
        PluginPublication(MavenPublication) {
            from components.java
            groupId 'com.scarlatti'
            artifactId 'launch4j-gradle-plugin'
            version project.version
        }
    }
}